package minijava.ast

typeprefix: MJ

abstract syntax:

Program(MainClass mainClass, ClassDeclList classDecls)

MainClass(String name, String argsName, Block mainBody)

ClassDecl(String name, Extended extended, VarDeclList fields, MethodDeclList methods)

Extended =
    ExtendsNothing()
  | ExtendsClass(String name)

VarDecl(Type type, String name)

MethodDecl(Type returnType, String name, VarDeclList formalParameters, Block methodBody)

MemberDecl =
    VarDecl
  | MethodDecl

Type =
    TypeArray(Type componentType)
  | BaseType

BaseType =
    TypeInt()
  | TypeBool()
  | TypeClass(String name)

Statement =
    Block
  | StmtIf(Expr condition, Statement ifTrue, Statement ifFalse)
  | StmtWhile(Expr condition, Statement loopBody)
  | StmtReturn(Expr result)
  | StmtPrint(Expr printed)
  | VarDecl
  | StmtExpr(Expr expr)
  | StmtAssign(ExprL address, Expr right)

// ExprL represents expressions that have an address
// and as such can be used on the left hand side of an assignment
ExprL =
    ArrayLookup(Expr arrayExpr, Expr arrayIndex)
  | FieldAccess(Expr receiver, String fieldName)
  | VarUse(String varName)


Expr =
    // reads the value from the given address
    Read(ExprL address)
  | ExprBinary(Expr left, Operator operator, Expr right)
  | ExprUnary(UnaryOperator unaryOperator, Expr expr)
  | ArrayLength(Expr arrayExpr)
  | MethodCall(Expr receiver, String methodName, ExprList arguments)
  | BoolConst(boolean boolValue)
  | Number(int intValue)
  | ExprThis()
  | ExprNull()
  | NewArray(Type baseType, Expr arraySize)
  | NewObject(String className)

Operator =
    And()
  | Plus()
  | Minus()
  | Times()
  | Div()
  | Less()
  | Equals()

UnaryOperator =
    UnaryMinus()
  | Negate()


// List types
ClassDeclList * ClassDecl
VarDeclList * VarDecl
MethodDeclList * MethodDecl
Block * Statement
ExprList * Expr



attributes:

"information about the source code"
frontend.SourcePosition Element.sourcePosition
